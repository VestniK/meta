{ '+' '-' } <<  // lower priority
{ '*' '/' } <<  // higher priority

Goal -> Package <eof>

Package -> 'package' (<qname> | <identifier>) ';' [Function]... +> Package

Function -> <identifier> <identifier> '(' [Arg]/','... ')' '{' (Statement)... '}' +> Function
Arg -> <identifier> <identifier> +> Arg

Statement -> <identifier> <identifier> ['=' Expr] ';' +> VarDecl
          -> Expr ';' +> ExprStatement
          -> 'return' Expr ';' +> Return

Expr -> UnaryExpr
     -> Expr '+' Expr +> BinaryOp
     -> Expr '-' Expr +> BinaryOp
     -> Expr '*' Expr +> BinaryOp
     -> Expr '/' Expr +> BinaryOp
     -> Call
     -> Var '=' Expr +> Assigment

UnaryExpr -> '+' UnaryExpr +> PrefixOp
          -> '-' UnaryExpr +> PrefixOp
          -> <num> +> Number
          -> Var  +> Var
          -> '(' Expr ')'

Var -> <identifier>

Call -> <identifier> '(' [Expr]/','... ')' +> Call
